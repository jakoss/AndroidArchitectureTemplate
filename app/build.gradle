plugins {
    alias libs.plugins.kotlin.kapt
    alias libs.plugins.android.application
    id 'project.android-base-conventions'
    id 'project.android-compose-conventions'
    alias libs.plugins.tangle
}

android {
    namespace 'pl.jsyty.architecturetemplate'

    defaultConfig {
        applicationId "pl.jsyty.architecturetemplate"
        versionCode 1
        versionName "1.0"

        buildConfigField "String", "API_URL", "\"https://api.open-meteo.com/v1\""
    }

    buildTypes {
        release {
            // You should change that to some internal key
            signingConfig signingConfigs.debug

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        internal {
            // You should change that to some internal key
            signingConfig signingConfigs.debug

            versionNameSuffix '-dbg'
        }
    }

    buildFeatures {
        buildConfig = true
    }
}

anvil {
    generateDaggerFactories = false
}

tangle {
    fragmentsEnabled = false
    workEnabled = false
    viewModelOptions {
        enabled = true
        composeEnabled = true
        fragmentsEnabled = false
        activitiesEnabled = false
    }
}

kapt {
    useBuildCache = true
}

dependencies {
    implementation project(':core:ui')
    implementation project(':core:infrastructure')
    implementation project(':core:networking')
    implementation project(':core:navigation')
    implementation project(':core:navigation:impl')

    implementation project(':features:dashboard')
    implementation project(':features:dashboard:impl')
    implementation project(':features:message:impl')
    implementation project(':features:longaction:impl')
    implementation project(':features:weather:impl')

    implementation project(':data:weather')
    implementation project(':data:weather:impl')

    kapt libs.dagger.compiler

    implementation libs.androidx.coordinatorlayour
    implementation libs.fragNav

    implementation libs.bundles.networking

    internalImplementation libs.bundles.pluto
}
