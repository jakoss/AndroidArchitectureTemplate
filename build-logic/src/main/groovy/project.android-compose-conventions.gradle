android {
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion libs.versions.compose.compiler.get()
    }

    // Added a property to reduce compilation bloat
    // Also, the compose compiler reports should be done only for release builds anyway
    if (findProperty("enableComposeCompilerReports") == "true") {
        kotlinOptions {
            freeCompilerArgs += [
                    "-P",
                    "plugin:androidx.compose.compiler.plugins.kotlin:reportsDestination=" +
                            layout.buildDirectory.absolutePath + "/compose_metrics",
                    "-P",
                    "plugin:androidx.compose.compiler.plugins.kotlin:metricsDestination=" +
                            layout.buildDirectory.absolutePath + "/compose_metrics"
            ]
        }
    }
}

dependencies {
    var composeBom = platform(libs.compose.bom)
    implementation composeBom

    implementation libs.androidx.activity.compose

    api libs.bundles.compose.ui.core

    lintChecks libs.compose.linter

    implementation(libs.whetstone.compose)

    androidTestImplementation composeBom
    androidTestImplementation libs.bundles.androidx.test
    androidTestImplementation libs.bundles.compose.test

    debugImplementation libs.bundles.compose.ui.debug
}
