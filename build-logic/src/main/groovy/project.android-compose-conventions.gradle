android {
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion libs.versions.compose.compiler.get()
    }

    // Added a property to reduce compilation bloat
    // Also, the compose compiler reports should be done only for release builds anyway
    if (findProperty("enableComposeCompilerReports") == "true") {
        kotlinOptions {
            freeCompilerArgs += [
                "-P",
                "plugin:androidx.compose.compiler.plugins.kotlin:reportsDestination=" +
                        buildDir.absolutePath + "/compose_metrics",
                "-P",
                "plugin:androidx.compose.compiler.plugins.kotlin:metricsDestination=" +
                        buildDir.absolutePath + "/compose_metrics"
            ]
        }
    }
}

dependencies {
    implementation libs.androidx.activity.compose

    implementation libs.bundles.compose.ui.core

    androidTestImplementation libs.bundles.androidx.test
    androidTestImplementation libs.bundles.compose.test

    debugImplementation libs.bundles.compose.ui.debug
}
