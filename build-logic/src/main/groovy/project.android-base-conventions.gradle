plugins {
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.parcelize'
    id 'com.squareup.anvil'
    id 'org.jmailen.kotlinter'
    id 'io.gitlab.arturbosch.detekt'
    id 'org.gradle.android.cache-fix'
}

android {
    compileSdk 33

    defaultConfig {
        minSdk 21
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildTypes {
        internal {
            initWith release
        }
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    buildFeatures {
        // we want this to be disabled for everything besides the app project for perforamnce reasons
        buildConfig = false
    }
}

anvil {
    generateDaggerFactories = true
}

detekt {
    parallel = true

    config = files(rootDir.absolutePath + "/config/detekt/detekt.yml")

    ignoredBuildTypes = ["release", "internal"]
}

dependencies {
    coreLibraryDesugaring libs.desugaring

    detektPlugins libs.detekt.rules.compose

    implementation libs.coroutines.lib

    implementation libs.dagger.core

    implementation libs.bundles.androidx.core
    implementation libs.timber
    testImplementation libs.junit4
}
