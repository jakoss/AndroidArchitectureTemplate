plugins {
    alias libs.plugins.kotlin.kapt
    alias libs.plugins.android.application
    id 'project.android-base-conventions'
    id 'project.android-compose-conventions'
    alias libs.plugins.whetstone
}

android {
    namespace 'pl.jsyty.architecturetemplate'

    defaultConfig {
        applicationId "pl.jsyty.architecturetemplate"
        versionCode 1
        versionName "1.0"

        buildConfigField "String", "API_URL", "\"https://api.open-meteo.com/v1\""
    }

    buildTypes {
        release {
            // You should change that to some internal key
            signingConfig signingConfigs.debug

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        internal {
            // You should change that to some internal key
            signingConfig signingConfigs.debug

            versionNameSuffix '-dbg'
        }
    }

    buildFeatures {
        buildConfig = true
    }

    lint {
        checkDependencies true
        sarifReport true
    }
}

kapt {
    useBuildCache = true
}

dependencies {
    implementation projects.core.ui
    api projects.core.infrastructure
    implementation projects.core.networking

    for (p in rootProject.subprojects) {
        if (p.path.endsWith(":impl") && p != project) {
            implementation p
        }
    }

    kapt libs.dagger.compiler

    implementation libs.androidx.coordinatorlayour
    implementation libs.fragNav

    implementation libs.orbitmvi.viewmodel
    implementation libs.material

    internalImplementation libs.bundles.pluto
}
